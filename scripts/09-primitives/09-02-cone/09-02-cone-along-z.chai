def scene_at(now)
{
  var camera = Cameras.perspective( [ "eye": pos(10,0,0),
                                      "look_at": pos(-2,0,0) ] )

  var wall_material = Materials.uniform( [ "ambient": Colors.white() * 0.1,
                                           "diffuse": Colors.white() * 0.8,
                                           "specular": Colors.black(),
                                           "specular_exponent": 0 ] )

  var material = Materials.uniform( [ "ambient": Colors.white() * 0.1,
                                      "diffuse": Colors.blue() * 0.8,
                                      "specular": Colors.white() * 0.5,
                                      "specular_exponent": 10 ] )

  var angle = Animations.animate(degrees(180), degrees(0), seconds(2))
  var back_wall = decorate(wall_material, translate( vec(0,0,-6), xy_plane()))
  var wall = decorate(wall_material, translate( vec(-6,0,0), yz_plane()))
  var floor = decorate(wall_material, translate( vec(0,-4,0), xz_plane()))
  var cone = decorate(material, rotate_around_x(angle[now], cone_along_z()))

  var root = union( [ back_wall, floor, wall, cone ] )

  var lights = [ Lights.omnidirectional( pos(5,5,5), Colors.white() * 0.5 ),
                 Lights.omnidirectional( pos(-5,5,-5), Colors.white() * 0.5 )
               ]

  create_scene(camera, root, lights)
}

var raytracer   = Raytracers.v6()

var renderer    = Renderers.standard( [ "width": 300,
                                       "height": 300,
                                       "sampler": Samplers.multijittered(2),
                                       "ray_tracer": raytracer ] )

pipeline( scene_animation(scene_at, seconds(2)),
          [ Pipeline.animation(15),
            Pipeline.renderer(renderer),
            Pipeline.wif(), 
            Pipeline.base64(), 
            Pipeline.stdout() ] )